plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.30'
    id 'maven-publish'
    id 'signing'
    id 'org.jetbrains.dokka' version '0.9.18'
}

group 'io.umehara'
version releaseVersion

sourceCompatibility = 1.8

test {
    useJUnit()
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jsoup:jsoup:1.11.3'

    testImplementation 'org.assertj:assertj-core:3.12.2'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.27.0'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'

}

dokka {
    outputFormat = 'javadoc'
    outputDirectory = 'build/javadoc'
}

signing {
    sign publishing.publications
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'ogmapper'
                    description 'Open Graph Html Tag Parser'
                    url 'https://github.com/iumehara/ogmapper'
                    scm {
                        connection 'scm:git:git://github.com/iumehara/ogmapper.git'
                        developerConnection 'scm:git:ssh://github.com:iumehara/ogmapper.git'
                        url 'https://github.com/iumehara/ogmapper/tree/master'
                    }
                    licenses {
                        license {
                            name 'MIT License'
                            url 'http://www.opensource.org/licenses/mit-license.php'
                        }
                    }
                    developers {
                        developer {
                            id 'iumehara'
                            name 'Ichizo Umehara'
                            email 'ichizo@umehara.io'
                            organization 'iumehara'
                            organizationUrl 'http://umehara.io'
                        }
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url ossrhUrl
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}